buildscript {
    ext {
        springBootVersion = '2.4.4'
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.0.1622"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'se.patrikerdes.use-latest-versions' version '0.2.7'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.sonarqube' version '2.7'
    id 'jacoco'
    id "com.github.kt3k.coveralls" version "2.8.2"
    id 'io.franzbecker.gradle-lombok' version '2.0'
    id 'net.ltgt.apt' version '0.21'
    id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.23'
    id "info.solidsoft.pitest" version '1.5.1' apply(false)
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
    id 'uk.gov.hmcts.java' version '0.12.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt-idea'
apply plugin: 'application'
apply plugin: "org.sonarqube"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'se.patrikerdes.use-latest-versions'

mainClassName = 'uk.gov.hmcts.ccd.definition.store.CaseDataAPIApplication'

dependencyCheck {
    suppressionFile = 'dependency-check-suppressions.xml'

    // Ignore these non-runtime configurations
    skipConfigurations = ['codacy']
}

//spring boot dependency override
//overriding some default spring boot deps because too old. Not generally recommended but needed to prevent runtime errors with version 6.4.2 of Elastic
ext['elasticsearch.version'] = '7.16.2'
ext['javax-validation.version'] = '2.0.1.Final'
ext['hibernate-validator.version'] = '6.0.20.Final'
ext['spring-security.version'] = '5.4.7'
ext['jackson.version'] = '2.13.2'
ext['snakeyaml.version'] = '1.26'
ext['postgresql.version'] = '42.2.16'
//overriding log4j2 default version 2.7 because of vulnerability issues
ext['log4j2.version'] = '2.17.1'
ext['spring-framework.version'] = '5.3.18'

// end

ext {
    junitJupiterVersion = '5.7.0'
    junitVintageVersion = '5.7.0'
    springCloudVersion = '2020.0.1'
    springfoxSwaggerVersion = '3.0.0'
    hamcrestVersion = '1.3'
    powermockVersion = '2.0.0-beta.5'
    reformLogging= '5.1.9'
    restAssuredVersion = '4.3.0!!'
    groovyVersion = '3.0.2!!'
    tomcatVersion = '9.0.58!!'
    hibernateVersion = '5.4.24.Final!!'
    feignJackson = '11.6'
    limits = [
            'instruction': 99,
            'branch'     : 99,
            'line'       : 99,
            'complexity' : 89,
            'method'     : 99,
            'class'      : 90
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// tag::repositories[]
allprojects {
    group 'uk.gov.hmcts.ccd.definition'
    version '1.2.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url  "https://jitpack.io"
        }
    }

    apply plugin: 'java'

    ext {
        elasticSearchVersion = '7.16.2'
    }

    dependencies {
        testCompile ("org.springframework.boot:spring-boot-starter-test:${springBootVersion}"){
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        compile  group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.13.2'
        compile 'net.minidev:json-smart:2.4.7'
    }

    // Global constraints
    dependencies {
        compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
        compile "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
        compile "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2"
        compile "com.fasterxml.jackson.core:jackson-databind:2.13.2.2"
        compile "com.fasterxml.jackson.core:jackson-core:2.13.2"
        compile "com.fasterxml.jackson.core:jackson-annotations:2.13.2"
        compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
        compile "org.hibernate:hibernate-core:${hibernateVersion}"
        // Overriding transitive dependency commons-compress due to CVE failures
        compile "org.apache.commons:commons-compress:1.21"
        // CVE-2021-28170
        compile "org.glassfish:jakarta.el:4.0.1"
        // CVE-2021-42550
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
        compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'
        testCompile "io.rest-assured:xml-path:${restAssuredVersion}"
        testCompile "io.rest-assured:json-path:${restAssuredVersion}"
        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
        testCompile "org.codehaus.groovy:groovy-xml:${groovyVersion}"
        testCompile "org.codehaus.groovy:groovy-json:${groovyVersion}"
        testCompile 'com.github.hmcts:fortify-client:1.2.0:all'
    }

    configurations.all {
        collect { configuration ->
            configuration.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        }
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.github.ben-manes.caffeine' && details.requested.name == 'caffeine') {
                    details.useVersion '2.8.5'
                }
            }
        }
    }
}

sourceCompatibility = 11
targetCompatibility = 11


// end::repositories[]

subprojects { subproject ->
    version = project.version

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: "com.github.kt3k.coveralls"
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'uk.gov.hmcts.java'

    configurations {
        providedRuntime

        aatCompile.extendsFrom(testCompile)
        aatRuntime.extendsFrom(testRuntime)
        cucumberRuntime.extendsFrom(functionalRuntime)
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    dependencies {
        implementation group: 'org.apache.poi', name: 'poi', version: '5.2.2'
        implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.2'
        implementation group: 'org.apache.poi', name: 'poi-scratchpad', version: '5.2.2'
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

        compile  group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.13.2'
        compile group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLogging
        compile group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLogging
        compile 'com.github.hmcts:properties-volume-spring-boot-starter:0.0.4'

        compile group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
        compile (group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3') {
            exclude group: 'com.google.guava', module: 'guava'
        }
        compile (group: 'com.github.hmcts', name: 'idam-java-client', version: '2.0.1') {
            exclude group: 'com.google.guava', module: 'guava'
        }

        compile "org.springframework.security:spring-security-web"
        compile "org.springframework.security:spring-security-config"
        compile "org.springframework.boot:spring-boot-starter-oauth2-client"
        compile "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

        implementation "io.github.openfeign:feign-httpclient:${feignJackson}"

        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-data-jpa')

        compile group: 'com.zaxxer', name: 'HikariCP', version: '4.0.2'
        compile group: 'org.jooq', name: 'jool-java-8', version: '0.9.14'
        runtime "org.elasticsearch:elasticsearch:${elasticSearchVersion}"
        runtime "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticSearchVersion}"
        compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.7.0'
        compile('org.springframework.boot:spring-boot-starter-cache')

        compile 'org.flywaydb:flyway-core:6.5.7'

        compile group: 'javax.inject', name: 'javax.inject', version: '1'

        compile (group: 'com.microsoft.azure', name: 'azure-storage', version: '8.0.0') {
            exclude group: 'com.google.guava', module: 'guava'
        }
        compile "org.bouncycastle:bcprov-jdk15on:1.61", "org.bouncycastle:bcpkix-jdk15on:1.61"

        // CVE-2021-29425
        compile group: 'commons-io', name: 'commons-io', version: '2.8.0'
        implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.0.1'

        // To avoid compiler warnings about @API annotations in JUnit5 code.
        testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'
        testCompile "org.postgresql:postgresql:42.2.16"
        testCompile "org.testcontainers:postgresql:1.15.1"
        testCompile "org.hamcrest:hamcrest-core:${hamcrestVersion}"
        testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
        testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        testRuntime "org.junit.vintage:junit-vintage-engine:${junitVintageVersion}"
        testCompile "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation('org.springframework.cloud:spring-cloud-starter-contract-stub-runner:2.1.0.RELEASE') {
            exclude group: 'com.github.tomakehurst', module: 'wiremock-standalone'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
        }
        testCompile ('com.github.tomakehurst:wiremock-jre8:2.25.1')
        testCompile ('org.springframework.cloud:spring-cloud-contract-wiremock:2.2.1.RELEASE') {
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
        }
        testCompile ("org.mockito:mockito-core:3.6.0") {
            exclude group: 'org.hamcrest'
        }
        testCompile "org.mockito:mockito-all:1.10.19"
        testCompile 'org.mockito:mockito-junit-jupiter:3.6.0'
        testCompile 'com.github.npathai:hamcrest-optional:2.0.0'
        testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
        testCompile "io.github.openfeign:feign-jackson:${feignJackson}"
        testCompile 'io.github.openfeign.form:feign-form:3.8.0'
        testCompile  group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.13.2'
        testCompile "org.exparity:hamcrest-date:2.0.4"
        testCompile "io.rest-assured:rest-assured:${restAssuredVersion}"
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: powermockVersion
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion

        testCompile group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.17.1_RDM-13140'
        testCompile group: 'com.github.hmcts', name: 'befta-fw', version: '8.7.2-Categories'

        testCompile group: 'commons-lang', name: 'commons-lang', version: '2.6'
        // https://mvnrepository.com/artifact/junit/junit
        compile group: 'junit', name: 'junit', version: '4.13.1'
        testCompile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'

    }

    // from https://github.com/junit-team/junit5/issues/1024'
    afterEvaluate {
        jacocoTestReport {
            reports {
                // XML required by coveralls and for the below coverage checks
                // and html are generated by default
                xml.enabled true
                html.enabled true
            }

            classDirectories.setFrom((classDirectories.files.collect {
                fileTree(dir: it, exclude: ['uk/gov/hmcts/ccd/definition/store/hikari/**'])
            }))

            doLast {
                def report = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
                logger.lifecycle("Checking coverage results: ${report}")

                def parser = new XmlParser()
                parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
                parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
                def results = parser.parse(report)

                def percentage = {
                    def covered = it.'@covered' as Double
                    def missed = it.'@missed' as Double
                    ((covered / (covered + missed)) * 100).round(2)
                }

                def counters = results.counter
                def metrics = [:]
                metrics << [
                        'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })
                ]


                def failures = []
                metrics.each {
                    def limit = limits[it.key]
                    if (it.value < limit) {
                        failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
                    }
                }

                if (failures) {
                    logger.quiet("------------------ Code Coverage Failed -----------------------")
                    failures.each {
                        logger.quiet(it)
                    }
                    logger.quiet("---------------------------------------------------------------")
                    throw new GradleException("Code coverage failed")
                } else{
                    logger.quiet("Passed Code Coverage Checks")
                }
            }
        }
    }

    test {
        environment("AZURE_APPLICATIONINSIGHTS_INSTRUMENTATIONKEY", "some-key")
        generateCucumberReports.enabled = false
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }

        reports {
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport
}
// end::subprojects[]

sonarqube {
    properties {
        property "sonar.junit.reportPaths", 'build/test-results/junit-platform'
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/junitPlatformTest.exec"
        property "sonar.exclusions", "**/entity/*.java,**/model/*.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java,**/AdminWebAuthorizationProperties.java,**/ProxyController.java,**/ProxyService.java"
        property "sonar.cpd.exclusions", "**/entity/*.java,**/model/*.java"
        property "sonar.jacoco.excludes", "**/entity/*.java,**/model/*.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/*Controller.java,**/WorkBasketUserDefaultService.java,**/*Exception.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java,**/ProxyController.java,**/ProxyService.java"
        property "sonar.coverage.exclusions", "**/entity/*.java,**/model/*.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/*Controller.java,**/WorkBasketUserDefaultService.java,**/*Exception.java,**/CaseTypeItem.java,**/ApplicationParams.java,**/CaseTypePresentationControllerService.java,**/DisplayService.java,**/DisplayItemAdapterService.java,**/DisplayApiPresentationControllerService.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java,**/JacksonUtils.java,**/SwaggerConfiguration.java,**/ProxyController.java,**/ProxyService.java"
        property "sonar.projectName", "ccd-definition-store-api"
        property "sonar.projectKey", "ccd-definition-store-api"
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

idea.project.settings {
    compiler {
        javac {
            javacAdditionalOptions "-parameters"
        }
    }
}

idea {
    project {
        ipr {
            withXml { provider ->
                // Get XML as groovy.util.Node to work with.
                def projectXml = provider.asNode()
                // Find compiler configuration component.
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                // Replace current annotationProcessing
                // that is part of the compiler configuration.
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

// this allows ./gradlew run
bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
    archiveName = 'case-definition-store-api.jar'
}

// Although there are no sources to compile, Jenkins pipeline needs the jar file to be here
dependencies {
    compile project(':application')
}

task smoke(type: Test, dependsOn: ":aat:smoke")

task functional(type: Test, dependsOn: ":aat:functional")

task codeCoverageReport(type: JacocoReport, dependsOn: allprojects*.test) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    allprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        csv.enabled = false
        html.enabled = true
        html.destination = file("${buildDir}/reports/jacoco/html")
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
    }
}

// for cnp pipeline
task dependencyCheck(dependsOn: dependencyCheckAnalyze)

configurations { codacy }

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}
task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/jacocoTestReport.xml"
    ]
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    // The line below prevents the build from failing if the Fortify scan detects issues
    ignoreExitValue = true
}

cucumberReports {
    outputDir = file("${rootDir}/target/cucumber")
    reports = files("${rootDir}/target/cucumber.json")
}
