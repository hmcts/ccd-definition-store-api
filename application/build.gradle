ext.applicationName = 'case-definition-store-api'
apply plugin: 'info.solidsoft.pitest'

dependencyCheck {
    suppressionFile = "${rootDir}/dependency-check-suppressions.xml"
}

jar {
    enabled = true
    archiveFileName = "${applicationName}"
    manifest {
        attributes 'Implementation-Title': "${applicationName}",
                'Implementation-Version': project.version
    }
}

bootJar {
    enabled = false
}

dependencies {
    implementation project(':app-insights')
    implementation project(':rest-api')
    implementation project(':excel-importer')
    implementation project(':elastic-search-support')
    implementation project(':repository')
    implementation project(':domain')
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: springDocVersion

    testImplementation project(":app-insights").sourceSets.main.output
    testImplementation project(":commons").sourceSets.main.output
}

rootProject.tasks.named("distTar") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("distZip") {
    duplicatesStrategy = 'include'
}

pitest {
    targetClasses = ['uk.gov.hmcts.ccd.definition.store.*',
                     'uk.gov.hmcts.ccd.definition.store.hikari.*']
    targetTests = ['uk.gov.hmcts.ccd.definition.store.*']
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml', 'json')
    outputFormats = ['XML', 'HTML', 'CSV']
    mutationThreshold = 0
    coverageThreshold = 0
    features = ["+CLASSLIMIT(limit[15])"]
    jvmArgs = ['-Xms1G', '-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
}
