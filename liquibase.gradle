configurations {
    liquibase
}

ext.hibernateVersion = '5.4.21.Final!!'

dependencies {
    liquibase group: 'org.liquibase.ext', name: 'liquibase-hibernate5', version: '3.10.2'
    liquibase "org.hibernate:hibernate-core:${hibernateVersion}"
    liquibase "org.hibernate:hibernate-envers:${hibernateVersion}"
}

//loading properties file.
Properties liquibaseProps = new Properties()
liquibaseProps.load(new FileInputStream("$projectDir/repository/src/main/resources/liquibase.properties"))

task liquibaseDiffChangelog(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=" + "$projectDir/repository/src/main/resources/db/changelog/db.changelog-diff-" + buildTimestamp() + ".xml"
    args "--referenceUrl=hibernate:spring:uk.gov.hmcts.ccd.definition.store.repository?dialect=uk.gov.hmcts.ccd.definition.store.repository.NewPostgreSQL94Dialect"
    args "--username=" + liquibaseProps.getProperty('username')
    args "--password=" + liquibaseProps.getProperty('password')
    args "--url=" + liquibaseProps.getProperty('url')
    args "--driver=" + liquibaseProps.getProperty('driver')
    args "diffChangeLog"
}

task migratePostgresDatabase(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    def urlString = project.hasProperty("dburl") ? "jdbc:postgresql://${dburl}&gssEncMode=disable" : liquibaseProps.getProperty('url')
    def user = project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
    def password = project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')

    args "--changeLogFile=./repository/src/main/resources/db/changelog/db.changelog-master.xml"
    args "--username=$user"
    args "--password=$password"
    args "--url=$urlString"
    args "--driver=" + liquibaseProps.getProperty('driver')
    args "update"
}

def buildTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}
