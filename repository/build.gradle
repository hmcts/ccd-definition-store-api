apply plugin: 'info.solidsoft.pitest'

dependencyCheck {
    suppressionFile = "${rootDir}/dependency-check-suppressions.xml"
}

jar {
    enabled = true
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
    archiveFileName = 'definition-store-repository.jar'
}

bootJar {
    enabled = false
}

ext {
    limits = [  // A lot of the model objects are not covered
        'class' : 57
    ]
}

dependencies {
    implementation project(':commons')
    implementation("org.springframework.boot:spring-boot-starter-data-jpa") {
        exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
    }
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client'
    implementation group: 'commons-beanutils', name: 'commons-beanutils'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: springDocVersion
    implementation group: 'com.google.guava', name: 'guava'
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api'
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.20.Final'
    implementation group: 'jakarta.el', name: 'jakarta.el-api', version: '6.0.1'
    implementation group: 'org.flywaydb', name: 'flyway-core'
    implementation group: 'com.vladmihalcea', name: 'hibernate-types-60'


    testImplementation group: 'org.assertj', name: 'assertj-core'
    testImplementation project(":commons").sourceSets.main.output
    testImplementation group: 'com.vladmihalcea', name: 'db-util', version: '1.0.5'
}

pitest {
    targetClasses = ['uk.gov.hmcts.ccd.definition.store.repository.*']
    targetTests = ['uk.gov.hmcts.ccd.definition.store.repository.*',
                   'uk.gov.hmcts.ccd.definition.store.utils.*'
    ]
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml', 'json')
    outputFormats = ['XML', 'HTML', 'CSV']
    mutationThreshold = 0
    coverageThreshold = 0
    features = ["+CLASSLIMIT(limit[15])"]
    jvmArgs = ['-Xms1G', '-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
}
