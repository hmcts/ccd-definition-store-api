apply plugin: 'com.github.spacialcircumstances.gradle-cucumber-reporting'
dependencies {
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '2.0.0'
    compile group: 'org.awaitility', name: 'awaitility', version: '3.1.6'
}

sourceSets {
    aat {
        java {
            srcDir('src/aat/java')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources {
            srcDir('src/aat/resources')
        }
    }
}

task smoke(type: Test) {
    systemProperty "http.proxyHost", System.getProperty('http.proxyHost')
    systemProperty "http.proxyPort", System.getProperty('http.proxyPort')
    systemProperty "https.proxyHost", System.getProperty('https.proxyHost')
    systemProperty "https.proxyPort", System.getProperty('https.proxyPort')

    environment("AZURE_APPLICATIONINSIGHTS_INSTRUMENTATIONKEY", "some-key")

    group = 'Delivery pipeline'
    description = 'Executes smoke tests against an AAT CCD Data Store instance'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "/aat/src/aat/java/uk/gov/hmcts/ccd/definitionstore/tests/functional/**"

    useJUnitPlatform {
        includeTags 'smoke'
    }

    doFirst {
        javaexec {
            main = "uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreTestDataLoader"
            classpath += sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = ['']
        }
    }
}

task functional(type: Test) {
    systemProperty "http.proxyHost", System.getProperty('http.proxyHost')
    systemProperty "http.proxyPort", System.getProperty('http.proxyPort')
    systemProperty "https.proxyHost", System.getProperty('https.proxyHost')
    systemProperty "https.proxyPort", System.getProperty('https.proxyPort')

    group = 'Delivery pipeline'
    description = 'Executes functional tests against an AAT CCD Definition Store Instance'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/ccd/definitionstore/tests/functional/**"
    useJUnitPlatform()
    
    generateCucumberReports.enabled = false

    doFirst {
        generateCucumberReports.enabled = true
        javaexec {
            main = "uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreBeftaMain"
            classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', "json:${rootDir}/target/cucumber.json", '--tags', 'not @Ignore', '--glue',
                    'uk.gov.hmcts.befta.player', 'src/aat/resources/features']
        }
    }
}
cucumberReports {
    outputDir = file("${rootDir}/target/cucumber")
    reports = files("${rootDir}/target/cucumber.json")
}
bootJar {
    enabled = false
}

jar {
    enabled = false
}
