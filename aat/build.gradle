apply plugin: 'com.github.spacialcircumstances.gradle-cucumber-reporting'
 //apply plugin: 'info.solidsoft.pitest'

dependencyCheck {
    suppressionFile = "${rootDir}/dependency-check-suppressions.xml"
}

sourceSets {
    aat {
        java {
            srcDir('src/aat/java')
            compileClasspath += main.output
            compileClasspath += main.compileClasspath
            compileClasspath += test.compileClasspath
            runtimeClasspath += main.output
            runtimeClasspath += main.runtimeClasspath
            runtimeClasspath += test.runtimeClasspath
        }
        resources {
            srcDir('src/aat/resources')
        }
    }
}

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntime.extendsFrom(testRuntimeOnly)
}

dependencies {
    implementation group: 'org.awaitility', name: 'awaitility', version: '3.1.6'
}

tasks.named("processAatResources") {
    duplicatesStrategy = 'include'
}

task highLevelDataSetup(type: JavaExec) {
    dependsOn aatClasses

    mainClass = "uk.gov.hmcts.ccd.definitionstore.befta.HighLevelDataSetupApp"
    classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task smoke(type: Test) {
    description = 'Executes smoke tests against the CCD Definition Store Instance just deployed'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/ccd/definitionstore/befta/**"
    useJUnitPlatform {
        includeTags 'smoke'
    }

    dependsOn aatClasses

    new File("$buildDir/test-results/test").mkdirs()
    copy {
        from "src/aat/resources/DummyTest.xml"
        into "$buildDir/test-results/test"
    }

    doLast {
        generateCucumberReports.enabled = true
        javaexec {
            main = "uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreBeftaMain"
            classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = [
                     '--plugin', "json:${rootDir}/target/cucumber.json",
                     '--tags', '@Smoke and not @Ignore',
                     '--glue', 'uk.gov.hmcts.befta.player', 'src/aat/resources/features'
                   ]
            jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]
        }
    }

    finalizedBy {
        generateCucumberReports {
            doLast {
                delete "${rootDir}/BEFTA Report for Smoke Tests/"
                new File("${rootDir}/BEFTA Report for Smoke Tests").mkdirs()
                file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Smoke Tests"))

                logger.quiet("Smoke test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Smoke%20Tests/overview-features.html")
            }
        }
    }

    outputs.upToDateWhen { false }
}

def tags = (findProperty('tags') == null) ? 'not @Ignore' : '(' + findProperty('tags') + ') and not @Ignore'

task functional(type: Test) {
    description = 'Executes functional tests against the CCD Definition Store Instance just deployed'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/ccd/definitionstore/tests/functional/**"
    useJUnitPlatform()

    dependsOn aatClasses

    new File("$buildDir/test-results/test").mkdirs()
    copy {
        from "src/aat/resources/DummyTest.xml"
        into "$buildDir/test-results/test"
    }

    generateCucumberReports.enabled = false

    doFirst {
        generateCucumberReports.enabled = true
        javaexec {
            mainClass = "uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreBeftaMain"
            classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = [
                     '--threads', '1', // current definition-store tests do not support multiple threads
                     '--plugin', "json:${rootDir}/target/cucumber.json",
                     '--tags', "${tags}",
                     '--glue', 'uk.gov.hmcts.befta.player', 'src/aat/resources/features'
                   ]
            jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]
        }
    }

    finalizedBy {
        generateCucumberReports {
            doLast{
                delete "${rootDir}/BEFTA Report for Functional Tests/"
                new File("${rootDir}/BEFTA Report for Functional Tests").mkdirs()
                file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Functional Tests"))

                logger.quiet("Functional test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Functional%20Tests/overview-features.html")
            }
        }
    }

    outputs.upToDateWhen { false }
}

// allows execution of individual functional tests from within IntelliJ wihout full execution of BEFTA
task functionalNoCucumberReport(type: Test) {
    description = 'Executes functional tests against the CCD Definition Store Instance just deployed'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    useJUnitPlatform()

    dependsOn aatClasses
}

cucumberReports {
    outputDir = file("${rootDir}/target/cucumber")
    reports = files("${rootDir}/target/cucumber.json")
}

task customCleanBeftaReports(type:Delete) {
    delete "${rootDir}/BEFTA Report for Smoke Tests/"
    delete "${rootDir}/BEFTA Report for Functional Tests/"
    delete "${rootDir}/target/cucumber"
    delete "${rootDir}/target/cucumber.json"
}
tasks.clean.dependsOn(tasks.customCleanBeftaReports)

bootJar {
    enabled = false
}

jar {
    enabled = false
}

/*pitest {
    targetClasses = ['uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreTestAutomationAdapter']
    excludedClasses = [
            'uk.gov.hmcts.ccd.definitionstore.befta.DefinitionStoreBeftaMain',
            'uk.gov.hmcts.ccd.definitionstore.befta.TestDataLoaderMain'
    ]
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = ['build/reports/pitest/fastermutationtesting']
    historyOutputLocation = ['build/reports/pitest/fastermutationtestingoutput']
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    mutationThreshold = 0
    coverageThreshold = 0
    features = ["+CLASSLIMIT(limit[15])"]
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
}*/

